*> extfunction.cob
*> Joshua Aidelman
*> 1000139
*> jaidelma@uoguelph.ca

IDENTIFICATION DIVISION.
PROGRAM-ID. extfunction.
ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
    SELECT STANDARD-OUTPUT ASSIGN TO DISPLAY.
DATA DIVISION.
FILE SECTION.

*> To print output
FD STANDARD-OUTPUT.
    01 OUT-LINE  PICTURE X(80).

WORKING-STORAGE SECTION.
  77 DIFF PICTURE V9(5).
  77 Z    PICTURE 9(11)V9(6).
  77 K    PICTURE S9999.
  77 X    PICTURE 9(11)V9(6).
  77 Y    PICTURE 9(11)V9(6).
  77 TEMP PICTURE S9(11)V9(6).

*> Print the square root calculation
01 PRINT-LINE.
   02 FILLER PICTURE X(19) VALUE 'The square root of '.
   02 OUT-Z  PICTURE Z(11)9.9(6).
   02 FILLER PICTURE X(4) VALUE ' is '.
   02 OUT-Y  PICTURE Z(11)9.9(6).

*> Print if there are too many iterations
01 ABORT-MESS.
   02 FILLER PICTURE X VALUE SPACE.
   02 OUTP-Z PICTURE Z(11)9.9(6).
   02 FILLER PICTURE X(37) VALUE
      '  ATTEMPT ABORTED,TOO MANY ITERATIONS'.

LINKAGE SECTION.
  77 IN-Z     PICTURE S9(10)V9(6) SIGN LEADING SEPARATE.
  77 IN-DIFF  PICTURE V9(5).

*> Start procedure
PROCEDURE DIVISION USING IN-Z, IN-DIFF.
    OPEN OUTPUT STANDARD-OUTPUT.

*> This function iterates the babylonian square root method
GETAPPROXIMATION.
    MOVE IN-DIFF TO DIFF.
    MOVE IN-Z TO Z.
    COMPUTE X ROUNDED = Z/2.
    PERFORM APPROXIMATE VARYING K FROM 1 BY 1
        UNTIL K IS GREATER THAN 1000.
    MOVE IN-Z TO OUTP-Z.
    WRITE OUT-LINE FROM ABORT-MESS AFTER ADVANCING 1 LINE.
    CLOSE STANDARD-OUTPUT.
    GOBACK.

*> This function does the calculations for each step
APPROXIMATE.
    COMPUTE Y ROUNDED = 0.5 * (X + Z / X).
    COMPUTE TEMP = X - Y.
    IF TEMP IS LESS THAN ZERO
      COMPUTE TEMP = - TEMP
    END-IF.
    IF TEMP / (Y + X) IS GREATER THAN DIFF
      MOVE Y TO X
      EXIT PARAGRAPH
    END-IF.
    MOVE IN-Z TO OUT-Z.
    MOVE Y TO OUT-Y.
    WRITE OUT-LINE FROM PRINT-LINE AFTER ADVANCING 1 LINE.
    MOVE Y TO X.
    CLOSE STANDARD-OUTPUT.
    GOBACK.
